buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

ext.configFile = file "project.properties"

configFile.withReader {
    // read config.  it shall from now on be referenced as simply config or as project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "${config.mod_version_major}.${config.mod_version_api}.${config.mod_version_minor}.${config.mod_version_patch}"
group = "${config.group_id}" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "ExtremeReactors-${config.minecraft_version}"

ext.dependencies = "required-after:Forge@[${config.forge_version_minDep},${config.forge_version_maxDep});required-after:zerocore@[${config.zerocore_version},)"

minecraft {
    version = "${config.forge_version}"


	if (file("../run-${config.minecraft_version}").exists()) {
		runDir = "../run-${config.minecraft_version}"
	} else {
		runDir = "run"
	}

    mappings = "${config.minecraft_mappings}"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {

    maven {
        name "zerocore"
        url "http://maven.zerono.it/"
    }

    maven {
        name "Tesla"
        url "http://maven.epoxide.xyz/"
    }

    maven { // The repo from which to get waila
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }

    maven { // TOP
        name 'tterrag maven'
        url "http://maven.tterrag.com/"
    }
}

dependencies {

    compile group: "it.zerono.mods.zerocore", name: "zerocore", version: "${config.zerocore_version}"
    deobfCompile group: "net.darkhax.tesla", name: "Tesla", version: "${config.tesla_version}"
    deobfCompile "mcp.mobius.waila:Waila:1.7.0-B3_1.9.4"
    deobfCompile "mcjty.theoneprobe:TheOneProbe:1.10-1.3.3-41"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

compileJava.doFirst {

    File file = new File(projectDir, 'build/sources/main/java/erogenousbeef/bigreactors/common/MobVats.java')
    String source = file.getText('UTF-8')
            .replaceAll('version = "0.0.0.0"', 'version = "' + project.version + '"')
            .replaceAll('acceptedMinecraftVersions = ""', 'acceptedMinecraftVersions = "' + config.minecraft_version + '"')
            .replaceAll('dependencies = ".*"', 'dependencies = "' + project.ext.dependencies + '"')

    file.write(source, 'UTF-8')
}